# git diff by files with fzf

emulate -L zsh

git rev-parse --is-inside-work-tree &> /dev/null || return

local preview_cmd files commit repo highlighter

[[ $# -ne 0 ]] && {
    if git rev-parse $1 -- &>/dev/null ; then
        commit=$1 && files=(${@:2})
    else
        files=($@)
    fi
}

if (( ${+commands[delta]} )); then
    highlighter='| delta'
elif (( ${+commands[diff-so-fancy]} )); then
    highlighter='| diff-so-fancy'
else
    highlighter=''
fi

repo=$(git rev-parse --show-toplevel)
preview_cmd="xargs -I% git --no-pager diff --color=always $commit -- '$repo/%' <<< {} $highlighter"

git --no-pager diff --name-only $commit -- ${files[*]} |
    fzf --exit-0 --preview-window=right:50% --no-sort --tac --layout=reverse-list --preview=$preview_cmd

# vim: ft=zsh
