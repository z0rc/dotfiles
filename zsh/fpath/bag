# recursively search for string, feed matches to fzf with preview, launch $EDITOR with selected match

emulate -L zsh

# use bat, if it's available...
local preview_cmd
if (( ${+commands[bat]} )); then
    preview_cmd='bat --style=numbers --color=always --highlight-line=${2} ${1}'
else
    # ...otherwise just highlight line with match using sed replace
    preview_cmd='sed -E "s/(.*'$*'.*)/'${bg[grey]}'\1'${bg[default]}'/gI;" < ${1}'
fi

# use whole bag input as single search query
# prefer rg over ag over grep
if (( ${+commands[rg]} )); then
    command rg --no-heading --line-number --smart-case --fixed-strings --color=always "$*"
elif (( ${+commands[ag]} )); then
    command ag --nogroup --color --silent "$*"
else
    command grep --line-number --recursive --ignore-case --color=always --no-messages "$*"
fi | fzf --no-sort --delimiter=: \
         --preview=$preview_cmd \
         --preview-window='right,50%,+{2}/2' \
         --bind='enter:become($EDITOR {1} +{2})'

# vim: ft=zsh
